// Defender devices (only truly onboarded) with AMA & SecurityEvent
(
DeviceInfo
| where OnboardingStatus == "Onboarded"
| summarize arg_max(TimeGenerated, OSPlatform) by DeviceName
| extend DeviceNameLower = tolower(DeviceName)
| join kind=leftouter (
    //Check for logs within 3 day timeframe. Change accordingly if you want to check a bigger timeframe.
    Heartbeat
    | where TimeGenerated > ago(3d)
    | summarize LastAMASeen = max(TimeGenerated) by Computer
    | extend ComputerLower = tolower(Computer)
    )
    on $left.DeviceNameLower == $right.ComputerLower
| join kind=leftouter (
    SecurityEvent
    //Check for logs within 3 day timeframe. Change accordingly if you want to check a bigger timeframe.
    | where TimeGenerated > ago(3d)
    | summarize LastSecurityEvent = max(TimeGenerated) by Computer
    | extend ComputerLower = tolower(Computer)
    )
    on $left.DeviceNameLower == $right.ComputerLower
| extend 
    OSPlatform = tostring(OSPlatform),
    HasAMA = iff(isnotempty(LastAMASeen), "Yes", "No"),
    SendsSecurityLogs = iff(isnotempty(LastSecurityEvent), "Yes", "No"),
    Source = "Defender Device"
| project
    //DeviceName,
    DeviceName = tostring(split(DeviceName, ".")[0]),
    OSPlatform,
    HasAMA,
    LastAMASeen,
    SendsSecurityLogs,
    LastSecurityEvent,
    Source
)
// AMA-only devices (Heartbeat present, NOT onboarded to Defender)
| union (
    Heartbeat
    //Check for logs within 3 day timeframe. Change accordingly if you want to check a bigger timeframe.
    | where TimeGenerated > ago(3d)
    | summarize 
        LastAMASeen = max(TimeGenerated),
        Latest = arg_max(TimeGenerated, OSType, OSName, Category)
        by Computer
    | extend ComputerLower = tolower(Computer)
    // exclude only devices that are actually ONBOARDED in DeviceInfo
    | where ComputerLower !in (
        DeviceInfo
    | where OnboardingStatus == "Onboarded"
    | summarize arg_max(TimeGenerated, OSPlatform) by DeviceName
    | extend DeviceNameLower = tolower(DeviceName)
    | project DeviceNameLower
    )
// join security events
| join kind=leftouter (
    SecurityEvent
    //Check for logs within 3 day timeframe. Change accordingly if you want to check a bigger timeframe.
    | where TimeGenerated > ago(3d)
    | summarize LastSecurityEvent = max(TimeGenerated) by Computer
    | extend ComputerLower = tolower(Computer)
    )
    on ComputerLower
| extend 
    DeviceName = Computer,
    // prefer full OSName if available
    OSPlatform = coalesce(OSName, OSType, Category, "Unknown"),
    HasAMA = "Yes",
    SendsSecurityLogs = iff(isnotempty(LastSecurityEvent), "Yes", "No"),
    Source = "AMA Only"
| project
    //DeviceName,
    DeviceName = tostring(split(DeviceName, ".")[0]),
    OSPlatform,
    HasAMA,
    LastAMASeen,
    SendsSecurityLogs,
    LastSecurityEvent,
    Source
)
// classify
| extend StatusCategory = case(
Source == "Defender Device" and HasAMA == "Yes", "MDE + AMA",
Source == "Defender Device" and HasAMA == "No", "MDE Only",
Source == "AMA Only", "AMA Only",
"Unknown"
)
// Removed server-only filter so all Windows 10/11 devices appear too
| order by Source asc, DeviceName asc
// HasAMA filter:
//| where HasAMA == 'Yes' or 'No'
//Filter OS Platform
//| where OSPlatform contains 'Server'